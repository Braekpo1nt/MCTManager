plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'org.braekpo1nt'
version = '3.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "onarandombox"
        url = "https://repo.onarandombox.com/content/groups/public/"
    }
    maven {
        name = "jitpack" // MockBukkit
        url = "https://jitpack.io/"
    }
    maven {
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "WorldEdit"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "PacketEvents1"
        url = uri("https://repo.codemc.io/repository/maven-releases/")
    }
    maven {
        name = "PacketEvents2"
        url = uri("https://repo.codemc.io/repository/maven-snapshots/")
    }
}

configurations {
    shade
}

dependencies {
    implementation("org.junit.jupiter:junit-jupiter:5.13.3")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") // needed to resolve discrepancy between this and MockBukkit
    testImplementation 'org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.64.0'

    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.slf4j:slf4j-jdk14:2.0.5'

    testImplementation 'io.papermc.paper:paper-api:1.21.7-R0.1-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper-api:1.21.7-R0.1-SNAPSHOT'
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.9.5'

    // This would normally be compileOnly, but it's implementation so that the MockBukkit
    // tests can work. I mock PacketEventsAPI in the test folders to be a no-op implementation.
    implementation 'com.github.retrooper:packetevents-spigot:2.9.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.sk89q.worldedit:worldedit-bukkit:7.4.0-SNAPSHOT'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.19'
    shade 'com.github.stefvanschie.inventoryframework:IF:0.11.2'
    
    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.jar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "spigot"
    }
}

tasks.shadowJar {
    configurations = [project.configurations.shade] // only dependencies labeled with the "shade" configuration will be included in the fat jar (alongside my own code)
    archiveClassifier = "" // doesn't add "-all" to the jar file name
    relocate 'com.github.stefvanschie.inventoryframework', 'org.braekpo1nt.inventoryframework' // from the inventoryFramework docs
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}

